local *

import declare_type from require 'quicktype'

declare_type 'Self', 'any'

import Args from require 'fat.args'
display = require 'fat.display.main'
ledger = require 'fat.ledger.main'
import run_tests from require 'spec'
import 'fat.logger'
import log from require 'fat.logger'
import 'spec'

main = (raw_args) ->
  args, ok = Args::parse raw_args
  if not ok
    return

  logger.set_log_verbosity args.verbose
  spec.set_log_verbosity args.verbose

  if args.test
    run_tests args.test.filter
    return
  if not args.no_install
    install_startup_script raw_args

  spam args.no_reattempt, ->
    if args.display?
      display.main args.display
    else if args.ledger?
      ledger.main args.ledger

install_startup_script = (raw_args) ->
  stringified_args = {}
  for arg in *raw_args
    if arg\match "'"
      error "args cannot contain \"'\": got #{arg}"
    stringified_args[] = "'#{arg}'"

  script = "
    shell.run('set motd.enable false')
    shell.execute({'fat', #{table.concat stringified_args, ', '}})
  "
  with assert io.open 'startup.lua', 'w+'
    \write script

spam = (no_reattempt, fn) ->
  if no_reattempt
    fn!
    return

  attempt = 1
  done = false
  while not done
    attempt += 1
    try
      fn!
      done = true
    catch err
      print err
      print "[failed]"
      sleep 1
      print "[attempt #{attempt}]"
      term?.clear!

sleep = (n_secs) ->
  if os.sleep?
    os.sleep n_secs
  else
    os.execute "sleep #{n_secs}s"

main {...}
