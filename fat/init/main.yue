local *

import Flag, Param, Subcommand from require 'clap'
import CONFIG_FILE from require 'fat.config'
import parse_toml from require 'fat.toml'
import F from require 'quicktype'

display = require 'fat.display.main'
station_config = require 'fat.station.config'
ledger = require 'fat.ledger.main'

export subcommand = with Subcommand 'init'
  \description 'initialise this fat instance'
  \add with Flag 'force'
    \description 'overwrite existing config'
  \add with Param 'instance-type'
    \description 'the type of instance to initialise'
    \options
      * 'station'
      * 'ledger'
      * 'display'

export main = F '({}) -> <>', (args) ->
  default_config = switch args.instance_type
    when 'station'
      station_config.default_config
    when 'ledger'
      ledger.default_config
    when 'display'
      display.default_config
    else
      error "internal error: unrecognised instance type '#{args.instance_type}'"

  if not args.force
    with? io.open CONFIG_FILE, 'r'
      \close!
      print "#{CONFIG_FILE} already exists"
      return

  temp_config_path = os.tmpname!
  with assert io.open temp_config_path, 'w+'
    assert \write default_config
    assert \close!

  config_is_valid = false
  while not config_is_valid
    while true
      print 'press [ENTER] edit fat.toml, [q] to abort'
      resp = io.read '*l'
      switch resp
        when 'q'
          print 'aborted'
          return
        when ''
          break

    if shell?
      shell.execute 'edit', temp_config_path
    else
      os.execute "nvim '#{temp_config_path}'"

    config_is_valid = validate_config temp_config_path, args.instance_type

  os_rename_assert = (...) ->
    if 0 == select '#', ...
      return
    assert ...
  os_rename_assert os.rename temp_config_path, CONFIG_FILE

  print "#{args.instance_type} configured"
  return

validate_config = F '(string, string) -> boolean', (file, instance_type) ->
  local raw_config
  with assert io.open file, 'r'
    raw_config = assert \read '*a'
    assert \close!

  config = parse_toml raw_config

  if not config[instance_type]?
    print "config does not contain #{instance_type} section"
    return false

  true
