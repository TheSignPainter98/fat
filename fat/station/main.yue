local *

import log from require 'fat.logger'
import Platform from require 'fat.peripheral.platform'
import Stockpile from require 'fat.peripheral.stockpile'
import IdempotenceToken, Packet, Uplink from require 'fat.peripheral.uplink'
import declare_type, F from require 'quicktype'
import spec from require 'spec'

export default_config = [=[
[station]
name = '<station-name>'

[[platforms]]
name = '<platform-name>'
network = 'mainline'
# produces = '<resource-name>'
# consumes = '<resource-name>'
]=]

export main = F '({}) -> <>', (config) ->
  print 'starting station'
  with Station config, {Platform!}, Stockpile!, Uplink!
    \run!

class Station
  new: F '({}, [Platform], Stockpile, Uplink) => <>', (@config, @platforms, @stockpile, @uplink) =>

  name: F '() => string', =>
    @config.station.name

  run: F '() => !', =>
    while true
      @step!

  step: F '() => <>', =>
    from_id, message = @uplink\receive_from_any!
    if not from_id?
      return

    switch message\protocol!
      when 'StockpileRequest'
        @on_stockpile_request from_id, message
      when 'ScheduleRequest'
        @on_schedule_request from_id, message
      when 'InfoRequest'
        @on_info_request from_id, message
      else
        print "ignoring #{message\protocol!} message"

  on_stockpile_request: F '(number, Packet) => <>', (requester, request) =>
    log -> 'handling stockpile request'

    info = @stockpile\info!
    @uplink\send_to requester, StockpileResponse request.idemp_tok, info

  on_schedule_request: F '(number, Packet) => <>', (requester, request) =>
    log -> 'handling schedule request'
    error 'todo'

  on_info_request: F '(number, Packet) => <>', (requester, request) =>
    log -> 'handling info request'

    stockpile_info = @stockpile\info!
    platform_infos = [ platform\info! for platform in *@platforms ]
    response = InfoResponse request.idemp_tok, @name!, platform_infos, stockpile_info
    @uplink\send_to requester, response

class StockpileRequest extends Packet
  new: F '(IdempotenceToken) => <>', (@idemp_tok) =>

class StockpileResponse extends Packet
  new: F '(IdempotenceToken, StockpileInfo) => <>', (@idemp_tok, @info) =>

class ScheduleRequest extends Packet
  new: F '(IdempotenceToken, Schedule) => <>', (@idemp_tok, @schedule) =>

class ScheduleResponse extends Packet
  new: F '(IdempotenceToken, ?string) => <>', (@idemp_tok, @error_reason) =>

class InfoRequest extends Packet
  new: F '(IdempotenceToken) => <>', (@idemp_tok) =>

class InfoResponse extends Packet
  new: F '(IdempotenceToken, string, [PlatformInfo], StockpileInfo) => <>', (@idemp_tok, @name, @platform_infos, @stockpile_info) =>

spec ->
  import TestPlatformBackend from require 'fat.peripheral.platform'
  import TestStockpileBackend from require 'fat.peripheral.stockpile'
  import TestUplinkBackend from require 'fat.peripheral.uplink'
  import assert_that, describe, expect_that, it, matchers from require 'spec'
  import deep_eq, eq, has_fields, len from matchers

  describe 'Station', ->
    it 'responds to stockpile requests', ->
      sent = {}
      SERVER_ID = 12345
      idemp_tok = IdempotenceToken!

      config = {}
      platforms =
        * Platform TestPlatformBackend!
      stockpile = Stockpile TestStockpileBackend
        size: => 1
        slot_content: (i) =>
          assert_that i, eq 1
          count: 32, displayName: 'Stone Bricks'
        slot_capacity: (i) =>
          assert_that i, eq 1
          64
      uplink = Uplink TestUplinkBackend
        receive: (_, _) =>
          @count ??= 1
          if @count == 1
            @count += 1
            return SERVER_ID, (StockpileRequest idemp_tok), StockpileRequest\protocol!
          nil, nil, nil
        send: (recipient, message, protocol) =>
          sent[] = :recipient, :message, :protocol
          true
      station = Station config, platforms, stockpile, uplink

      for _ = 1, 10
        station\step!

      expect_that sent, len eq 1
      expect_that sent[1], has_fields
        recipient: eq SERVER_ID
        message: has_fields
          idemp_tok: eq idemp_tok
          info: has_fields
            item: eq 'Stone Bricks'
            stored: eq 32
            capacity: eq 64
        protocol: eq StockpileResponse\protocol!

    it 'responds to info requests', ->
      sent = {}
      SERVER_ID = 12345
      idemp_tok = IdempotenceToken!

      config =
        station:
          name: 'test-station-name'
      platforms =
        * Platform TestPlatformBackend
            name: => 'test-platform-name'
            train_name: =>
              @count ??= 1
              if @count == 1
                @count += 1
                'test-train-name'
              else
                nil
      stockpile = Stockpile TestStockpileBackend
        size: => 1
        slot_content: (i) =>
          assert_that i, eq 1
          count: 32, displayName: 'Stone Bricks'
        slot_capacity: (i) =>
          assert_that i, eq 1
          64
      uplink = Uplink TestUplinkBackend
        receive: (_, _) =>
          @count ??= 1
          if @count <= 2
            @count += 1
            SERVER_ID, (InfoRequest idemp_tok), InfoRequest\protocol!
          else
            nil, nil, nil
        send: (recipient, message, protocol) =>
          sent[] = :recipient, :message, :protocol
          true
      station = Station config, platforms, stockpile, uplink

      for _ = 1, 10
        station\step!

      expect_that sent, len eq 2
      expect_that sent[1], has_fields
        recipient: eq SERVER_ID
        message: has_fields
          idemp_tok: eq idemp_tok
          name: eq 'test-station-name'
          platform_infos: deep_eq
            * name: 'test-platform-name'
              train_name: 'test-train-name'
          stockpile_info: has_fields
            item: eq 'Stone Bricks'
            stored: eq 32
            capacity: eq 64
        protocol: eq InfoResponse\protocol!
      expect_that sent[2], has_fields
        recipient: eq SERVER_ID
        message: has_fields
          idemp_tok: eq idemp_tok
          name: eq 'test-station-name'
          platform_infos: deep_eq
            * name: 'test-platform-name'
              train_name: nil
          stockpile_info: has_fields
            item: eq 'Stone Bricks'
            stored: eq 32
            capacity: eq 64
        protocol: eq InfoResponse\protocol!
